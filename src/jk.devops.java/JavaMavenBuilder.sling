
/*
 * This file is part of Jkop
 * Copyright (c) 2020-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.log
import jk.env
import jk.process

class:

property mvnCommand as File
{
}

func getMvnCommandFile as File
{
	var v = mvnCommand
	if v:
		return v
	v = ExternalCommand.findInPath("mvn")
	if v:
		return v
	return null
}

func getBuilderId as string:
	return "javaMaven"

func canBuild(dir as File) as bool
{
	assert dir
	if dir.entry("pom.xml").isFile():
		return true
	return false
}

func execute(ctx as LoggingContext, sourceDirectory as File, destinationDirectory as File) as bool
{
	var dir = assert sourceDirectory:
		Log.error(ctx, "No source directory")
	assert dir.isDirectory():
		Log.error(ctx, "Source directory does not exist: `" .. String.asString(dir) .. "'")
	Log.debug(ctx, "Using source directory: `" .. String.asString(dir) .. "'")
	var mvnExe = assert getMvnCommandFile():
		Log.error(ctx, "Failed to find `mvn' executable")
	Log.debug(ctx, "Using maven command: `" .. String.asString(mvnExe) .. "'")
	var pomFile = dir.entry("pom.xml")
	assert pomFile.isFile():
		Log.error(ctx, "Source directory does not have a pom.xml")
	var errorBuffer as StringBuilder
	if ctx.isInDebugMode():
		errorBuffer = new StringBuilder()
	var pl = ProcessLauncher.forFile(mvnExe)
	pl.addToParams("package")
	pl.setErrorBuffer(errorBuffer)
	pl.setCwd(dir)
	Log.debug(ctx, "Executing maven command: `" .. pl.toString() .. "'")
	var r = pl.execute()
	if r != 0 {
		Log.error(ctx, "Maven build returned an error status: " .. String.forInteger(r))
		if errorBuffer && errorBuffer.count() > 0:
			Log.error(ctx, "Maven execution error buffer: `" .. errorBuffer.toString() .. "'")
		return false
	}
	else {
		if errorBuffer && errorBuffer.count() > 0:
			Log.debug(ctx, "Maven execution error buffer: `" .. errorBuffer.toString() .. "'")
	}
	Log.debug(ctx, "Maven build succesful")
	var target = dir.entry("target")
	if target && destinationDirectory {
		var jarFile as File
		var iter = target.entries()
		while iter {
			var ff = iter.next()
			if not ff:
				break
			if ff.hasExtension("jar") {
				jarFile = ff
				break
			}
		}
		if jarFile {
			var dest = destinationDirectory.entry(jarFile.baseName())
			Log.debug(ctx, "Copying `" .. String.asString(jarFile) .. "' -> `" .. String.asString(dest) .. "' ..")
			assert jarFile.copyFileTo(dest):
				Log.error(ctx, "Failed to copy `" .. String.asString(jarFile) .. "' -> `" .. String.asString(dest) .. "'")
		}
		else {
			Log.error(ctx, "Failed to find generated jar file to copy to output directory")
		}
	}
	return true
}
